-- bd
CREATE DATABASE InventarioDB;
GO

USE InventarioDB;
GO

--tabla MOV_INVENTARIO
CREATE TABLE MOV_INVENTARIO (
	  COD_CIA VARCHAR(5) NOT NULL
	, COMPANIA_VENTA_3 VARCHAR(5) NOT NULL
	, ALMACEN_VENTA VARCHAR(10) NOT NULL
	, TIPO_MOVIMIENTO VARCHAR(2) NOT NULL
	, TIPO_DOCUMENTO VARCHAR(2) NOT NULL
	, NRO_DOCUMENTO VARCHAR(50) NOT NULL
	, COD_ITEM_2 VARCHAR(50) NOT NULL
	, PROVEEDOR VARCHAR(100) NULL
	, ALMACEN_DESTINO VARCHAR(50) NULL
	, CANTIDAD INT NULL
	, DOC_REF_1 VARCHAR(50) NULL
	, DOC_REF_2 VARCHAR(50) NULL
	, DOC_REF_3 VARCHAR(50) NULL
	, DOC_REF_4 VARCHAR(50) NULL
	, DOC_REF_5 VARCHAR(50) NULL
	, FECHA_TRANSACCION DATE NULL
	, CONSTRAINT PK_MOV_INVENTARIO PRIMARY KEY CLUSTERED
	  (
	  COD_CIA ASC,
	  COMPANIA_VENTA_3 ASC,
	  ALMACEN_VENTA ASC,
	  TIPO_MOVIMIENTO ASC,
	  TIPO_DOCUMENTO ASC,
	  NRO_DOCUMENTO ASC,
	  COD_ITEM_2 ASC
	  )
);
GO

EXEC SP_CONSULTAR_MOVIMIENTOS @FechaInicio='2026-02-18',@FechaFin='2026-02-18'
--sp consulta registros con filtros opcionales
CREATE OR ALTER PROCEDURE SP_CONSULTAR_MOVIMIENTOS
	  @FechaInicio DATE = NULL
	, @FechaFin DATE = NULL
	, @TipoMovimiento VARCHAR(2) = NULL
	, @NroDocumento VARCHAR(50) = NULL
AS
BEGIN
	SET NOCOUNT ON;

	SELECT COD_CIA
		 , COMPANIA_VENTA_3
		 , ALMACEN_VENTA
		 , TIPO_MOVIMIENTO
		 , TIPO_DOCUMENTO
		 , NRO_DOCUMENTO
		 , COD_ITEM_2
		 , PROVEEDOR
		 , ALMACEN_DESTINO
		 , CANTIDAD
		 , DOC_REF_1
		 , DOC_REF_2
		 , DOC_REF_3
		 , DOC_REF_4
		 , DOC_REF_5
		 , FECHA_TRANSACCION
	FROM dbo.MOV_INVENTARIO
	WHERE (@FechaInicio IS NULL OR FECHA_TRANSACCION >= @FechaInicio)
		AND (@FechaFin IS NULL OR FECHA_TRANSACCION <= @FechaFin)
		AND (@TipoMovimiento IS NULL OR TIPO_MOVIMIENTO = @TipoMovimiento)
		AND (@NroDocumento IS NULL OR NRO_DOCUMENTO = @NroDocumento)
	ORDER BY FECHA_TRANSACCION DESC;
END
GO

--sp insertar 
CREATE OR ALTER PROCEDURE SP_INSERTAR_MOVIMIENTO
	  @COD_CIA VARCHAR(5)
	, @COMPANIA_VENTA_3 VARCHAR(5)
	, @ALMACEN_VENTA VARCHAR(10)
	, @TIPO_MOVIMIENTO VARCHAR(2)
	, @TIPO_DOCUMENTO VARCHAR(2)
	, @NRO_DOCUMENTO VARCHAR(50)
	, @COD_ITEM_2 VARCHAR(50)
	, @PROVEEDOR VARCHAR(100) = NULL
	, @ALMACEN_DESTINO VARCHAR(50) = NULL
	, @CANTIDAD INT = NULL
	, @DOC_REF_1 VARCHAR(50) = NULL
	, @DOC_REF_2 VARCHAR(50) = NULL
	, @DOC_REF_3 VARCHAR(50) = NULL
	, @DOC_REF_4 VARCHAR(50) = NULL
	, @DOC_REF_5 VARCHAR(50) = NULL
	, @FECHA_TRANSACCION DATE = NULL
AS
BEGIN
	SET NOCOUNT ON;

	BEGIN TRY
		INSERT INTO dbo.MOV_INVENTARIO (COD_CIA, COMPANIA_VENTA_3, ALMACEN_VENTA, TIPO_MOVIMIENTO, TIPO_DOCUMENTO, NRO_DOCUMENTO, COD_ITEM_2, PROVEEDOR, ALMACEN_DESTINO, CANTIDAD, DOC_REF_1, DOC_REF_2, DOC_REF_3, DOC_REF_4, DOC_REF_5, FECHA_TRANSACCION)
		VALUES (@COD_CIA, @COMPANIA_VENTA_3, @ALMACEN_VENTA, @TIPO_MOVIMIENTO, @TIPO_DOCUMENTO, @NRO_DOCUMENTO, @COD_ITEM_2, @PROVEEDOR, @ALMACEN_DESTINO, @CANTIDAD, @DOC_REF_1, @DOC_REF_2, @DOC_REF_3, @DOC_REF_4, @DOC_REF_5, @FECHA_TRANSACCION);

		SELECT 1 AS Result
			 , 'Registro insertado correctamente' AS Message;
	END TRY
	BEGIN CATCH
		SELECT 0 AS Result
			 , ERROR_MESSAGE() AS Message;
	END CATCH

END
GO

--sp actualizar
CREATE OR ALTER PROCEDURE SP_ACTUALIZAR_MOVIMIENTO
	  @COD_CIA VARCHAR(5)
	, @COMPANIA_VENTA_3 VARCHAR(5)
	, @ALMACEN_VENTA VARCHAR(10)
	, @TIPO_MOVIMIENTO VARCHAR(2)
	, @TIPO_DOCUMENTO VARCHAR(2)
	, @NRO_DOCUMENTO VARCHAR(50)
	, @COD_ITEM_2 VARCHAR(50)
	, @PROVEEDOR VARCHAR(100) = NULL
	, @ALMACEN_DESTINO VARCHAR(50) = NULL
	, @CANTIDAD INT = NULL
	, @DOC_REF_1 VARCHAR(50) = NULL
	, @DOC_REF_2 VARCHAR(50) = NULL
	, @DOC_REF_3 VARCHAR(50) = NULL
	, @DOC_REF_4 VARCHAR(50) = NULL
	, @DOC_REF_5 VARCHAR(50) = NULL
	, @FECHA_TRANSACCION DATE = NULL
AS
BEGIN
	SET NOCOUNT ON;

	BEGIN TRY
		UPDATE dbo.MOV_INVENTARIO
		SET PROVEEDOR = @PROVEEDOR
		  , ALMACEN_DESTINO = @ALMACEN_DESTINO
		  , CANTIDAD = @CANTIDAD
		  , DOC_REF_1 = @DOC_REF_1
		  , DOC_REF_2 = @DOC_REF_2
		  , DOC_REF_3 = @DOC_REF_3
		  , DOC_REF_4 = @DOC_REF_4
		  , DOC_REF_5 = @DOC_REF_5
		  , FECHA_TRANSACCION = @FECHA_TRANSACCION
		WHERE COD_CIA = @COD_CIA
			AND COMPANIA_VENTA_3 = @COMPANIA_VENTA_3
			AND ALMACEN_VENTA = @ALMACEN_VENTA
			AND TIPO_MOVIMIENTO = @TIPO_MOVIMIENTO
			AND TIPO_DOCUMENTO = @TIPO_DOCUMENTO
			AND NRO_DOCUMENTO = @NRO_DOCUMENTO
			AND COD_ITEM_2 = @COD_ITEM_2;

		IF @@rowcount > 0
			SELECT 1 AS Result
				 , 'Registro actualizado correctamente' AS Message;
		ELSE
			SELECT 0 AS Result
				 , 'No se encontró el registro para actualizar' AS Message;
	END TRY
	BEGIN CATCH
		SELECT 0 AS Result
			 , ERROR_MESSAGE() AS Message;
	END CATCH

END
GO

--sp eliminar 
CREATE PROCEDURE SP_ELIMINAR_MOVIMIENTO
	  @COD_CIA VARCHAR(5)
	, @COMPANIA_VENTA_3 VARCHAR(5)
	, @ALMACEN_VENTA VARCHAR(10)
	, @TIPO_MOVIMIENTO VARCHAR(2)
	, @TIPO_DOCUMENTO VARCHAR(2)
	, @NRO_DOCUMENTO VARCHAR(50)
	, @COD_ITEM_2 VARCHAR(50)
AS
BEGIN
	SET NOCOUNT ON;

	BEGIN TRY
		DELETE FROM dbo.MOV_INVENTARIO
		WHERE COD_CIA = @COD_CIA
			AND COMPANIA_VENTA_3 = @COMPANIA_VENTA_3
			AND ALMACEN_VENTA = @ALMACEN_VENTA
			AND TIPO_MOVIMIENTO = @TIPO_MOVIMIENTO
			AND TIPO_DOCUMENTO = @TIPO_DOCUMENTO
			AND NRO_DOCUMENTO = @NRO_DOCUMENTO
			AND COD_ITEM_2 = @COD_ITEM_2;

		IF @@rowcount > 0
			SELECT 1 AS Result
				 , 'Registro eliminado correctamente' AS Message;
		ELSE
			SELECT 0 AS Result
				 , 'No se encontró el registro para eliminar' AS Message;
	END TRY
	BEGIN CATCH
		SELECT 0 AS Result
			 , ERROR_MESSAGE() AS Message;
	END CATCH

END
GO